plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.2'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'com.diffplug.spotless' version '7.0.2'
	id 'com.github.node-gradle.node' version '7.1.0'
}

group = 'de.fll'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.mysql:mysql-connector-j:9.2.0'
	implementation 'org.jsoup:jsoup:1.18.3'
    runtimeOnly 'com.h2database:h2:2.3.232'


    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

node {
	download = true
	npmVersion = '11.1.0'
	version = '22.11.0'
	workDir = file("${projectDir}/src/main/webapp")
}

tasks.register('appNpmInstall', NpmTask) {
	description = "read packge.json and install all jars"
	workingDir = file("${project.projectDir}/src/main/webapp")
	args = ["install"]
}
tasks.register('appNpmBuild', NpmTask) {
	description = "Builds client application"
	workingDir = file("${project.projectDir}/src/main/webapp")
	args = ["run", "build"]
}

tasks.register('copyClientFiles', Copy) {
	from "$projectDir/src/main/webapp/out/"
	into "$projectDir/src/main/resources/static"
}

appNpmBuild.dependsOn appNpmInstall
copyClientFiles.dependsOn appNpmBuild
compileJava.dependsOn copyClientFiles
processResources.dependsOn copyClientFiles

tasks.named('test') {
	useJUnitPlatform()
}

spotless {
    java {
        eclipse("4.28").configFile "spotless-style.xml"
        // fix formatting of type annotations
        formatAnnotations()
        trimTrailingWhitespace()
    }
}
